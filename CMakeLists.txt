cmake_minimum_required(VERSION 3.20.0)

include(CheckCCompilerFlag)
include(CheckFunctionExists)
include(CheckLanguage)
include(CheckLibraryExists)
include(CheckLinkerFlag)

project(ffmpeg C CXX ASM)

set(CMAKE_C_STANDARD 17)

include_directories(cmake)
include_directories(.)
include_directories(compat/stdbit)

function(check_enable_option option)
  string(REGEX REPLACE "^--*" "" option_name ${option})
  string(TOUPPER ${option_name} option_name)
  string(REGEX REPLACE "[-=]" "_" option_name ${option_name})
  set(varname "SUPPORTS_${option_name}")
  check_c_compiler_flag(${option} ${varname})
  if (${varname})
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${option}" PARENT_SCOPE)
  endif()
endfunction()

function(check_disable_warning warning)
  string(REGEX REPLACE "^-Wno-" "" warning ${warning})
  string(TOUPPER ${warning} warning_name)
  string(REGEX REPLACE "[-=]" "_" warning_name ${warning_name})
  set(varname "SUPPORTS_W${warning_name}")
  check_c_compiler_flag(-W${warning} ${varname})
  if (${varname})
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wno-${warning}" PARENT_SCOPE)
  endif()
endfunction()

# TODO: Check for -fno-tree-vectorize with GCC

if (NOT MSVC)
  # clang-cl supports -Wall, but it corresponds to -Weverything
  check_enable_option(-Wall)
endif()

check_enable_option(-Wdeclaration-after-statement)
check_enable_option(-Wdisabled-optimization)
check_enable_option(-Wpointer-arith)
check_enable_option(-Wredundant-decls)
check_enable_option(-Wwrite-strings)
check_enable_option(-Wtype-limits)
check_enable_option(-Wundef)
check_enable_option(-Wmissing-prototypes)
check_enable_option(-Wstrict-prototypes)
check_enable_option(-Wempty-body)
check_enable_option(-Werror=implicit-function-declaration)
check_enable_option(-Werror=missing-prototypes)
check_enable_option(-Werror=return-type)

# GCC specific
check_enable_option(-Werror=format-security)
check_enable_option(-Werror=vla)
check_enable_option(-Wformat)

check_disable_warning(-Wno-parentheses)
check_disable_warning(-Wno-switch)
check_disable_warning(-Wno-format-zero-length)
check_disable_warning(-Wno-pointer-sign)
check_disable_warning(-Wno-unused-const-variable)
check_disable_warning(-Wno-bool-operation)
check_disable_warning(-Wno-char-subscripts)

# Unless enabled extra_warnings
check_disable_warning(-Wno-maybe-uninitialized)

# Specific for the CMake build
check_disable_warning(-Wno-deprecated)
check_disable_warning(-Wno-implicit-const-int-float-conversion)

check_function_exists(sin HAVE_DEFAULT_MATH)
if(NOT HAVE_DEFAULT_MATH)
  check_library_exists(m sin "" HAVE_LIBM)
  if (HAVE_LIBM)
    link_libraries(m)
  endif()
endif()

if (WIN32)
  add_compile_definitions(WIN32_LEAN_AND_MEAN)
endif()

if (CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64" OR CMAKE_SYSTEM_PROCESSOR STREQUAL "arm64")
  set(CMAKE_SYSTEM_PROCESSOR "aarch64")
elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "^arm")
  set(CMAKE_SYSTEM_PROCESSOR "arm")
elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "^i.86$" OR CMAKE_SYSTEM_PROCESSOR MATCHES "^[Xx]86$")
  check_language(ASM_NASM)
  if(CMAKE_ASM_NASM_COMPILER)
    enable_language(ASM_NASM)
  else()
    add_compile_definitions(NO_X86ASM)
  endif()
  set(CMAKE_SYSTEM_PROCESSOR "i386")
  set(CMAKE_ASM_NASM_FLAGS "${CMAKE_ASM_NASM_FLAGS} -Pasm-i386.asm")
  if (CMAKE_SYSTEM_NAME STREQUAL "Darwin" OR CMAKE_SYSTEM_NAME STREQUAL "Windows")
    set(CMAKE_ASM_NASM_FLAGS "${CMAKE_ASM_NASM_FLAGS} -DPREFIX")
  endif()
  # If the option is supported, allow text relocations. (LLD for ELF disallows
  # it by default.)
  check_linker_flag(C -Wl,-z,notext SUPPORTS_WL_Z_NOTEXT)
  if (SUPPORTS_WL_Z_NOTEXT)
    add_link_options(-Wl,-z,notext)
  endif()
elseif(CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64" OR CMAKE_SYSTEM_PROCESSOR STREQUAL "AMD64")
  add_compile_definitions(PIC)
  check_language(ASM_NASM)
  if(CMAKE_ASM_NASM_COMPILER)
    enable_language(ASM_NASM)
  else()
    add_compile_definitions(NO_X86ASM)
  endif()
  set(CMAKE_SYSTEM_PROCESSOR "x86_64")
  set(CMAKE_ASM_NASM_FLAGS "${CMAKE_ASM_NASM_FLAGS} -Pasm-x86_64.asm")
  if (CMAKE_SYSTEM_NAME STREQUAL "Darwin")
    set(CMAKE_ASM_NASM_FLAGS "${CMAKE_ASM_NASM_FLAGS} -DPREFIX")
  endif()
endif()
add_subdirectory(libavutil)
add_subdirectory(libavcodec)
add_subdirectory(libavformat)
add_subdirectory(libpostproc)
add_subdirectory(libswscale)
add_subdirectory(libswresample)
add_subdirectory(libavfilter)
add_subdirectory(libavdevice)
add_subdirectory(fftools)
add_subdirectory(tests/checkasm)
